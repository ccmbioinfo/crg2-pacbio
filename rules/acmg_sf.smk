acmg_sf_input_report_type = [
    "wgs.coding.CH",
    "wgs.high.impact.CH",
    "sv.CH",
    "cnv.CH",
]

rule add_acmg_sf_columns:
    input:
        report="reports/{family}.{input_report_type}.csv",
        acmg_sf_list=config["annotation"]["general"]["acmg_sf_list"],
    output:
        report="reports/{family}.{input_report_type}.SF.csv",
    params:
        acmg_sf_version=config["annotation"]["general"]["acmg_sf_version"],
        seq_type="long",
    wildcard_constraints:
        input_report_type="|".join([t.replace(".", r"\.") for t in acmg_sf_input_report_type]),
    log:
        "logs/report/acmg_sf/{family}.{input_report_type}.SF.log",
    conda:
        "../envs/common.yaml"
    script:
        "../scripts/add_acmg_sf_columns.py"

rule create_acmg_sf_report:
    input:
        reports=lambda wildcards: expand(
            "reports/{family}.{input_report_type}.SF.csv",
            family=wildcards.family,
            input_report_type=acmg_sf_input_report_type,
        ),
    output:
        report="reports/{family}.ACMG.SF.csv",
    params:
        acmg_sf_version=config["annotation"]["general"]["acmg_sf_version"],
        seq_type="long",
    log:
        "logs/report/acmg_sf/{family}.acmg_sf_report.log",
    conda:
        "../envs/common.yaml"
    script:
        "../scripts/create_acmg_sf_report.py"